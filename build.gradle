apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "application"

// Main class of betsy
mainClassName = "betsy.Main"

repositories {
    mavenCentral()
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy', 'src/main/tests', 'src/main/java']
        }
        resources {
            srcDirs = ['src/main/groovy', 'src/main/resources', 'src/main/xslt', 'src/main/tests']
        }
    }
}

ext {
    props = new Properties()
    props.load(new File(project.rootDir, "config.properties").newInputStream())
}

configurations {
    internal
    external
}

dependencies {
    internal 'org.codehaus.groovy:groovy-all:2.3.3'

    internal 'org.apache.ant:ant:1.9.4'
    internal 'log4j:log4j:1.2.17'
    internal 'org.virtualbox:vboxjws:4.2.8'
    internal 'commons-daemon:commons-daemon:1.0.15'
    internal 'org.apache.commons:commons-io:1.3.2'
    internal 'commons-lang:commons-lang:2.6'
    internal 'org.apache.ant:ant-apache-log4j:1.9.2'

    testCompile 'junit:junit:4.11'

    external files(fileTree(dir: "${props.getProperty("soapui.home")}/bin", includes: ['*.jar']))
    external files(fileTree(dir: "${props.getProperty("soapui.home")}/lib", includes: ['*.jar'], excludes: ['groovy*', 'log4j*', 'commons-io*', 'commons-lang*']))

    compile configurations.internal
    compile configurations.external
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

task downloadSoapUI {
    outputs.file new File("${props.getProperty("downloads.dir")}/${props.getProperty("soapui.download.filename")}")

    doLast {
        ant.mkdir dir: props.getProperty("downloads.dir")
	// maxtime is set to this value to ensure that travis-ci can download this file
        ant.get dest: props.getProperty("downloads.dir"), maxtime: 1524, verbose: true, {
            ant.url url: props.getProperty("soapui.download.url")
        }
    }
}

task installSoapUI {
    ext.srcFile = new File("${props.getProperty("downloads.dir")}/${props.getProperty("soapui.download.filename")}")
    ext.destDir = props.getProperty("soapui.container")

    inputs.file srcFile
    outputs.dir destDir

    doLast {
        ant.mkdir dir: props.getProperty("soapui.home")
        ant.unzip src: srcFile, dest: destDir
    }
}

task downloadAnt {
    outputs.file new File("${props.getProperty("downloads.dir")}/${props.getProperty("ant.download.filename")}")

    doLast {
        ant.mkdir dir: props.getProperty("downloads.dir")
        ant.get dest: props.getProperty("downloads.dir"), maxtime: 120, {
            ant.url url: props.getProperty("ant.download.url")
        }
    }
}

task installAnt {
    ext.srcFile = new File("${props.getProperty("downloads.dir")}/${props.getProperty("ant.download.filename")}")
    ext.destDir = props.getProperty("ant.container")

    inputs.file srcFile
    outputs.dir props.getProperty("ant.home")

    doLast {
        ant.mkdir dir: destDir
        ant.unzip src: srcFile, dest: destDir
    }

}

installSoapUI.dependsOn downloadSoapUI
compileGroovy.dependsOn installSoapUI
installAnt.dependsOn downloadAnt
installSoapUI.dependsOn installAnt

run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

task enginecontrol(type: JavaExec, dependsOn: build) {
    description = 'Starts Engine Control GUI'
    classpath = sourceSets.main.runtimeClasspath
    main = "betsy.tool.EngineControl"
}

task partnerservice(type: JavaExec, dependsOn: build) {
    description = 'Starts test case partner service only'
    classpath = sourceSets.main.runtimeClasspath
    main = "betsy.tool.PartnerServiceControl"
}

test {
    testLogging {
        exceptionFormat "full" // default is "short"
    }
}

task internalClasses(dependsOn: classes) {
    inputs.files configurations.internal
    outputs.dir "build/internalClasses"

    doLast {
        copy {
            from { configurations.internal.collect { it.isDirectory() ? it : zipTree(it) } }
            into "build/internalClasses"
        }
    }
}

task zipSingleTestRun(type: Zip) {
    zip64 true

    into("test") {
        from files("test")
    }
    from files("betsy.log")
    from files("betsy_console.log")
    from files("betsy_time.log")

    if(project.hasProperty("zipArchiveBaseName")) {
        baseName = project.zipArchiveBaseName.toString()
    } else {
        // use default
        baseName = "test-folder-and-logs"
    }
}

/**
 * Creates jar file for the virtual machines.
 */
task jarServer(type: Jar, dependsOn: [classes, internalClasses]) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.resources)
    from files("build/internalClasses")

    // results in build/libs/betsy-vms.jar
    classifier = 'vms'
    baseName = 'betsy'

    manifest {
        attributes 'Main-Class': 'betsy.virtual.server.BetsyVirtualMachineServer', 'Implementation-Title': 'Betsy-VirtualMachineServer'
    }
}

task install(type: JavaExec, dependsOn: jarServer) {
    description = 'Install virtual machines to use the virtual engines'
    classpath = sourceSets.main.runtimeClasspath
    main = "betsy.tool.VirtualMachineInstaller"

    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}
